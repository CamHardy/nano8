program -> declaration* EOF ;

declaration -> variableDeclaration
						 | statement ;

variableDeclaration -> 'var' IDENTIFIER ( '=' expression )? ';' ;

statement -> expressionStatement
					 | ifStatement
					 | printStatement 
					 | block ;

expressionStatement -> expression ';' ;
ifStatement -> 'if' '(' expression ')' statement ( 'else' statement )? ;
printStatement -> 'print' expression ';' ;
block -> '{' declaration* '}' ;

expression -> assignment ;

assignment -> IDENTIFIER '=' assignment 
						| logic_or ;

logic_or -> logic_and ( 'or' logic_and )* ;
logic_and -> equality ( 'and' equality )* ;

equality -> comparison ( ( '==' | '!=' ) comparison )* ;
comparison -> term ( ( '>' | '>=' | '<' | '<=' ) term )* ;
term -> factor ( ( '-' | '+' ) factor )* ;
factor -> unary ( ( '/' | '*' ) unary )* ;

unary -> ( '!' | '-' ) unary ;
primary -> 'true' | 'false' | 'nil' | NUMBER | STRING | IDENTIFIER | '(' expression ')' ;

NUMBER -> DIGIT+ ( '.' DIGIT+ )? ;
STRING -> '"' <any char except '"'>* '"' ;
IDENTIFIER -> ALPHA ( ALPHA | DIGIT )* ;
ALPHA -> 'a' ... 'z' | 'A' ... 'Z' | '_' ;
DIGIT -> '0' ... '9' ;